* Depack routine for FlashPack 2.1
* By Fox of Taquart, 1st May 1997
* Average speed: 30 kB/sec

* 4 bytes on page 0
ff	equ	$fc
bt	equ	$fd
ad	equ	$fe

	opt h-
	org $8000

* Do not start routine here!!!
dep1	tax
	beq	exit
	lda	#$7f
dep2	bcc	*+3
	inx
	inx
	sta	ad
dep3	lda	(ad),y
put	sta	$8080,y
	iny
	bne	dep4
	inc	ad+1
	inc	put+2
reloc_1 equ *-2
dep4	dex
	bne	dep3
	asl	bt
	bne	dep7
	asl	ff
	bne	dep5

* Routine starts here!
start	equ	*
	ift NO_INT
	sei
	inc	$d40e
	lda #$fe
	sta $d301
	eif
	sec
	jsr	get
reloc_2 equ *-2
	rol	@
	sta	ff
dep5	lda	#1
	bcc	dep6
	jsr	get
reloc_3 equ *-2
	rol	@
dep6	sta	bt
dep7	jsr	get
reloc_4 equ *-2
	ldx	#1
	bcc	put
	lsr	@
	bne	dep2
	jsr	get
reloc_5 equ *-2
	bcs	dep1
	tay
	jsr	get
reloc_6 equ *-2
	sta	ad+1
	sta	put+2
reloc_7 equ *-2
	bcc	dep7 !

* Set address of packed data here!
* (or make your own "get" routine)
get	lda	$ffff
src_addr equ *-2
	inc	get+1
reloc_8 equ *-2
	bne	ret
	inc	get+2
reloc_9 equ *-2
ret
	ift !NO_INT
exit rts
	els
	rts
exit inc $d301
	lsr	$d40e
	cli
	rts
	eif

	end of code
